plugins {
    id 'java'
    id 'idea'
    id 'antlr'
}

group 'raylras.zenls'
ext.moduleName = 'raylras.zenls'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.1'
    implementation 'org.antlr:antlr4-runtime:4.13.1'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.21.1'
    implementation('org.slf4j:slf4j-api:2.0.9')
    implementation 'org.slf4j:slf4j-simple:2.0.9'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main'
    }
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += ['-visitor', '-listener', '-long-messages', '-package', 'raylras.zen.code.parser']
    outputDirectory = new File(buildDir.toString() + '/generated-src/antlr/main/raylras/zen/code/parser')
}

task cleanServer(type: Delete) {
    delete './vscode-extension/server/*'
}

task copyServerJar(type: Copy) {
    dependsOn jar
    from "${buildDir}/libs/"
    into './vscode-extension/server'
    include "${project.name}-${project.version}.jar"
}

task copyServerDependencies(type: Copy) {
    from project.configurations.runtimeClasspath
    into './vscode-extension/server'
    include "antlr4-runtime-*.jar"
    include "org.eclipse.lsp4j-*.jar"
    include "org.eclipse.lsp4j.jsonrpc-*.jar"
    include "gson-*.jar"
    include 'slf4j-api-*.jar'
    include 'slf4j-simple-*.jar'
}

task dist() {
    dependsOn build, cleanServer, copyServerJar, copyServerDependencies
}

